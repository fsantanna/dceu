data :Clock = [ms]

func {{+}} (v1, v2) {
    ifs {
        (type(v1) == :number) and (type(v2) == :number) => `:number ($v1.Number + $v2.Number)`
        (type(v1) == :tag)    and (type(v2) == :number) => `:tag    ($v1.Tag    + $v2.Number)`
        (type(v1) == :number) and (type(v2) == :tag)    => `:tag    ($v1.Number + $v2.Tag)`
        else => error(:error)
    }
}

func {{>=}} (v1,v2) {
    ifs {
        (type(v1) == :number) and (type(v2) == :number) => `:bool ($v1.Number >= $v2.Number)`
        (type(v1) == :tag)    and (type(v2) == :tag)    => `:bool ($v1.Tag    >= $v2.Tag)`
        else => error(:error)
    }
}

func xx-string? (v) {
    (type(v)==:vector) and (#v>=1) and (type(v[0])==:char)
}

func xx-cat-move (v1, v2) {
    loop i in {0 => #v2{ {
        set v1[+] = v2[i]
    }
    v1
}

func assert (v, msg) {
    ifs {
        v => v
        type(msg)==:func  => error(:error, msg())
        msg->xx-string?() => error(:error, xx-cat-move(xx-cat-move(#[], "assertion error : "), msg))
        (msg == nil)      => error(:error, "assertion error")
        else => error(:error, msg)
    }
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ARITHMETIC: +  -  *  **  /  //  %
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val [{{-}}, {{*}}, {{**}}, {{/}}, {{//}}, {{%}}] = group {
    func sub (v1, v2) {
        ifs {
            (type(v1) == :number) and (v2 == nil)           => `:number (- $v1.Number)`
            (type(v1) == :number) and (type(v2) == :number) => `:number ($v1.Number - $v2.Number)`
            (type(v1) == :tag)    and (type(v2) == :number) => `:tag    ($v1.Tag    - $v2.Number)`
            (type(v1) == :tag)    and (type(v2) == :tag)    => `:number ($v1.Tag    - $v2.Tag)`
            (type(v1) == :number) and (type(v2) == :tag)    => `:tag    ($v1.Number - $v2.Tag)`
            else                                            => error(:error)
        }
    }
    func mul (v1, v2) {
        if (type(v1) /= :number) or (type(v2) /= :number) {
            error(:error)
        }
        `:number ($v1.Number * $v2.Number)`
    }
    func exp (v1, v2) {
        if (type(v1) /= :number) or (type(v2) /= :number) {
            error(:error)
        }
        `:number powf($v1.Number, $v2.Number)`
    }
    func div (v1, v2) {
        if (type(v1) /= :number) or (type(v2) /= :number) {
            error(:error)
        }
        `:number ($v1.Number / $v2.Number)`
    }
    func div' (v1, v2) {
        if (type(v1) /= :number) or (type(v2) /= :number) {
            error(:error)
        }
        `:number powf($v1.Number, 1/$v2.Number)`
    }
    func rem (v1, v2) {
        if (type(v1) /= :number) or (type(v2) /= :number) {
            error(:error)
        }
        `:number fmod($v1.Number, $v2.Number)`
    }
    [sub, mul, exp, div, div', rem]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; RELATIONAL:  >=  >  <=  <
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val [{{<}}, {{<=}}, {{>}}] = group {
    func lt (v1,v2) {
        not (v1 >= v2)
    }
    func lte (v1,v2) {
        (v1 == v2) or lt(v1,v2)
    }
    func gt (v1,v2) {
        not lte(v1,v2)
    }
    [lt, lte, gt]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ITERATORS: tuple, dict, x-coro, coro, tasks
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

data :Iterator = [f,s,i,tp]
val to-iter = group {
    func iter-ret (itr :Iterator, idx) {
        val' ret = tuple(#itr.tp)
        loop j in {0 => #itr.tp{ {
            set ret[j] = match itr.tp[j] {
                == :idx => idx
                == :key => idx
                == :val => itr.s[idx]
            }
        }
        if #itr.tp==1 => ret[0] => ret
    }

    func iter-tuple (itr :Iterator) {
        val i = itr.i
        if i >= #itr.s {
            nil
        } else {
            set itr.i = i + 1
            iter-ret(itr, i)
        }
    }

    func iter-dict (itr :Iterator) {
        val k = next-dict(itr.s,itr.i)
        if k == nil {
            nil
        } else {
            set itr.i = k
            iter-ret(itr, k)
        }
    }

    func iter-xcoro (itr :Iterator) {
        val' co = itr.s
        (resume co()) --> func (x) {
            if (status(co) == :terminated) {
                nil
            } else {
                x
            }
        }
    }

    func iter-func (itr :Iterator) {
        itr.s(itr)
    }

    func iter-coro (itr :Iterator) {
        set itr.s = coroutine(itr.s)
        set itr.f = iter-xcoro
        itr.f(itr)
    }

    func iter-tasks (itr :Iterator) {
        ;;error(:TODO)
        set itr.i = next-tasks(itr.s,itr.i)
        itr.i
    }

    func (v, tp) {
        func tp' (def) {
            match type(tp) {
                == :nil   => def
                == :tuple => tp
                else      => [tp]
            }
        }
        match type(v) {
            | sup?(:Iterator, tag(v)) {
                val' v' :Iterator = v       ;; TODO: where
                if #v' >= 4 {
                    set v'.tp = tp'(v'.tp)
                }
                v'
            }
            ==:tuple    => :Iterator [iter-tuple, v, 0, tp'([:val])]
            ==:vector   => :Iterator [iter-tuple, v, 0, tp'([:val])]
            ==:dict     => :Iterator [iter-dict,  v, nil, tp'([:key,:val])]
            ==:func     => :Iterator [iter-func,  v, nil, nil]
            ==:coro     => :Iterator [iter-coro, v, nil, nil]
            ==:exe-coro => :Iterator [iter-xcoro, v, nil, nil]
            ==:tasks    => :Iterator [iter-tasks, v, nil, nil]
            else        => error(:error, "invalid collection")
        }
    }
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; === =/=
;; IS, IS-NOT
;; IN, IN-NOT
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val [{{===}}, {{=/=}}] = group {
    func eq (v1,v2) {
        val t1 = type(v1)
        val t2 = type(v2)
        ifs {
            v1 == v2 => true
            t1 /= t2 => false
            (t1==:tuple) or (t1==:vector) => equals-tuple-vector(v1,v2)
            t1 == :dict => contains-dict?(v1,v2) and contains-dict?(v2,v1)
            else => false
        } where {
            func equals-tuple-vector (v1', v2') {
                (#v1' == #v2') and not (
                    loop [i,v] in to-iter(v1',[:idx,:val]) {
                        while eq(v2'[i], v)
                    }
                )
            }
            func contains-dict? (v1', v2') {
                not (
                    loop [k,v] in to-iter(v1',[:key,:val]) {
                        while eq(v2'[k], v)
                    }
                )
            }
         }
    }
    func neq (v1,v2) {
        not eq(v1,v2)
    }
    [eq, neq]
}

val [is', is-not'] = group {
    func x-is (v1,v2) {
        ifs {
            (v1 === v2)        => true
            (type(v1) == v2)   => true
            (type(v2) == :tag) => sup?(v2, tag(v1))
            else => false
        }
    }
    func x-is-not (v1,v2) {
        not x-is(v1,v2)
    }
    [x-is, x-is-not]
}

val [in', in-not'] = group {
    func x-in (v, xs) {
        if (type(xs) == :dict) and xs[v] { \it =>
            it
        } else {
            loop [a,b] in to-iter(xs,[:key,:val]) {
                until match xs {
                    :dict   => (v === a) and b
                    :tuple  => (v === b) and b
                    :vector => (v === b) and b
                    else    => (v === a) and a
                }
            }
        }
    }
    func x-in-not (v, xs) {
        not x-in(v,xs)
    }
    [x-in, x-in-not]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TYPE-*
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;
data :Type = [
    string?, static?, dynamic?,
    ;;nil?, tag?, bool?, char?, number?, pointer?,
    ;;func?, coro?, task?,
    ;;tuple?, vector?, dict?,
    ;;exe-coro?, exe-task?,
    ;;tasks?,
]
;;;

val [static?, dynamic?, string?] = group {
    func x-static? (tp) {
        `:bool ($tp.Tag < CEU_VALUE_DYNAMIC)`
    }
    func x-dynamic? (tp) {
        `:bool ($tp.Tag > CEU_VALUE_DYNAMIC)`
    }
    ;;;
    func x-f (tp) {
        func (v) {
            type(v) == tp
        }
    }
    [
        x-static?, x-dynamic?, xx-string?,
        x-f(:nil), x-f(:tag), x-f(:bool), x-f(:char), x-f(:number), x-f(:pointer),
        x-f(:func), x-f(:coro), x-f(:task),
        x-f(:tuple), x-f(:vector), x-f(:dict),
        x-f(:exe-coro), x-f(:exe-task),
        x-f(:tasks),
    ]
    ;;;
    [x-static?, x-dynamic?, xx-string?]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TO: string, bool, char, number, tag, pointer, iter
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

data :To = [string, bool, char, number, tag, pointer, tuple, vector, dict, iter]

val to :To = group {
    func to-string (v) {
        match v {
            {{string?}} => v
            :nil        => "nil"
            :tag        => to-string-tag(v)
            :number     => to-string-number(v)
            :char       => #[v]
            :pointer    => to-string-pointer(v)
            else        => nil
        }
    }
    func to-bool (v) {
        not (not v)
    }
    func to-char (v) {
        match v {
            :char   => v
            :number => `:char $v.Number`
            {{string?}} {
                ifs {
                    (#v == 1) => v[0]
                    (#v /= 2) => nil
                    (v[0] /= '\\') => nil
                    else {
                        val t = @[
                            ("\\a", '\a'),
                            ("\\b", '\b'),
                            ("\\f", '\f'),
                            ("\\n", '\n'),
                            ("\\r", '\r'),
                            ("\\t", '\t'),
                            ("\\v", '\v'),
                            ("\\'", '\''),
                            ("\\\"", '\"'),
                            ("\\?", '\?'),
                            ("\\\\", '\\'),
                        ]
                        v in? t
                    }
                }
            }
            else => nil
        }
    }
    func to-number (v) {
        match v {
            :number     => v
            :tag        => `:number $v.Tag`
            :char       => `:number $v.Char`
            {{string?}} => `:number atoi($v.Dyn->Vector.buf)`
            else        => nil
        }
    }
    func to-tag (v) {
        match v {
            :tag        => v
            :number     => `:tag $v.Number`
            {{string?}} => to-tag-string(v)
            else        => nil
        }
    }
    func to-pointer (v) {
        match v {
            :pointer => v
            :tag     => `:pointer ceu_tag_to_pointer($v.Tag)`
            :tuple   => `:pointer $v.Dyn->Tuple.buf`
            :vector  => `:pointer $v.Dyn->Vector.buf`
            :dict    => `:pointer $v.Dyn->Dict.buf`
            else     => nil
        }
    }
    func to-tuple (col) {
        match col {
            :tuple => col
            :vector {
                val tup = tuple(#col)
                loop [i,v] in to-iter(col, [:idx,:val]) {
                    set tup[i] = v
                }
                drop(tup)
            }
            else => error(:TODO)
        }
    }
    func to-vector (col, tp) {
        val ret = #[]
        loop v in to-iter(col,tp) {
            set ret[+] = v
        }
        drop(ret)
    }
    func to-dict (col) {
        val ret = @[]
        loop [k,v] in to-iter(col,[:key,:val]) {
            val [k',v'] =
                if type(v) == :tuple {
                    [v[0], v[1]]    ;; [(:x,1),(:y,2)]
                } else {
                    [v, k or true]  ;; [:x,:y]
                }
            set ret[k'] = v'
        }
        drop(ret)
    }
    ;;;
    func to-tasks (...) {
        val ret = #[]
        val args = ...
        loop v in args {
            match v {
                :exe-task {
                    set ret[+] = v
                }
                :tasks {
                    loop i in v {
                        set ret[+] = i
                    }
                }
            }
        }
        ret
    }
    ;;;
    [to-string, to-bool, to-char, to-number, to-tag, to-pointer, to-tuple, to-vector, to-dict, to-iter]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; CONCAT:  <++  ++
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

val [{{<++}}, {{++}}] = group {
    func x-cat-move (v1, v2) {
        assert(type(v1) == :vector)
        assert(type(v2) == :vector)
        xx-cat-move(v1, v2)
    }
    func x-cat-copy (v1, v2) {
        x-cat-move(x-cat-move(#[], v1), v2)
    }
    [x-cat-move, x-cat-copy]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; RANDOM: seed, next
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

data :Random = [seed,next,bool,int]

val random :Random = group {
    func x-seed (n) {
        if n {
            `srand($n.Number);`
        } else {
            `srand(time(NULL));`
        }
    }
    func x-next () {
        `:number rand()`
    }
    func x-bool () {
        (x-next() % 2) == 0
    }
    func x-int (x, y) {
        ifs {
            (x==nil) and (y==nil) => x-next()
            (y == nil)            => x-next() % x               ;; {0,y{
            else                  => x + (x-next() % ((y+1)-x)) ;; {x,y}
        }
    }
    [x-seed, x-next, x-bool, x-int]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; MATH: sin,cos,floor , min,max,between
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

data :Math = [
    PI, sin, cos,
    floor, ceil, round,
    min, max, between,
]

val math :Math = group {
    val x-PI = `:number M_PI`
    func x-sin (v) {
        `:number sin($v.Number)`
    }
    func x-cos (v) {
        `:number cos($v.Number)`
    }

    func x-floor (v) {
        `:number floor($v.Number)`
    }
    func x-ceil (v) {
        `:number ceil($v.Number)`
    }
    func x-round (v) {
        `:number round($v.Number)`
    }

    func x-min (v1, v2) {
        ((v1 < v2) and v1) or v2
    }
    func x-max (v1, v2) {
        ((v1 > v2) and v1) or v2
    }
    func x-between (min, v, max) {
        x-max(min, x-min(v, max))
    }

    [x-PI,x-sin,x-cos , x-floor,x-ceil,x-round , x-min,x-max,x-between]
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; MISC: tag-or, next, create-resume, copy, assert
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func tag-or (t1,t2) {
    assert(type(t1) == :tag)
    assert(type(t2) == :tag)
    `:tag $t1.Tag | $t2.Tag`
}

func next (v, x) {
    match v {
        :exe-coro => resume v(x)
        :tasks    => next-tasks(v, x)
        :dict     => next-dict(v, x)
        :Iterator => v.f(v, x)
        else      => error(:error)
    }
}

func create-resume (CO, x, y, z) {
    val' co = coroutine(CO)         ;; TODO: where
    val v = resume co(x,y,z)
    assert(v == nil)
    co
}

func copy (v) {
    match type(v) {
        {{static?}} {
            v
        }
        :tuple {
            val ret = tuple(#v)
            loop i in {0 => #v{ {
                set ret[i] = copy(v[i])
            }
            drop(ret)
        }
        :vector {
            val ret = #[]
            loop i in {0 => #v{ {
                set ret[i] = copy(v[i])
            }
            drop(ret)
        }
        :dict {
            val ret = @[]
            var k = nil
            loop {
                set k = next-dict(v, k)
                until (k == nil)
                set ret[copy(k)] = copy(v[k])
            }
            drop(ret)
        }
        else {
            error(:TODO)
        }
    }
}
