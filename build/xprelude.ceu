;; is', is-not'

func is' (v1,v2) {
    ifs {
        (v1 == v2)         -> true
        (type(v2) /= :tag) -> false
        (type(v1) == v2)   -> true
        tags(v1,v2)        -> true
        else -> false
    }
}

func is-not' (v1,v2) {
    not is'(v1,v2)
}

;; ARITHMETIC:  +  -  *  /  %  **  //

func {+} (v1, v2) {
    if (v1 is-not? :number) or (v2 is-not? :number) {
        throw(:error)
    }
    `:number ($v1.Number + $v2.Number)`
}

func {-} (v1, v2) {
    ifs {
        (v1 is? :number) and (v2 is? :nil) -> {
            `:number (- $v1.Number)`
        }
        (v1 is-not? :number) or (v2 is-not? :number) -> {
            throw(:error)
        }
        else -> {
            `:number ($v1.Number - $v2.Number)`
        }
    }
}

func {*} (v1, v2) {
    if (v1 is-not? :number) or (v2 is-not? :number) {
        throw(:error)
    }
    `:number ($v1.Number * $v2.Number)`
}

func {**} (v1, v2) {
    if (v1 is-not? :number) or (v2 is-not? :number) {
        throw(:error)
    }
    `:number powf($v1.Number, $v2.Number)`
}

func {/} (v1, v2) {
    if (v1 is-not? :number) or (v2 is-not? :number) {
        throw(:error)
    }
    `:number ($v1.Number / $v2.Number)`
}

func {//} (v1, v2) {
    if (v1 is-not? :number) or (v2 is-not? :number) {
        throw(:error)
    }
    `:number powf($v1.Number, 1/$v2.Number)`
}

func {%} (v1, v2) {
    if (v1 is-not? :number) or (v2 is-not? :number) {
        throw(:error)
    }
    `:number fmod($v1.Number, $v2.Number)`
}

;; RELATIONAL:  >=  >  <=  <

func {>} (v1,v2) {
    ifs {
        (v1 is? :tag)    and (v2 is? :tag)    -> `:bool ($v1.Tag    > $v2.Tag)`
        (v1 is? :number) and (v2 is? :number) -> `:bool ($v1.Number > $v2.Number)`
        else -> throw(:error)
    }
}

func {<} (v1,v2) {
    not ((v1 == v2) or (v1 > v2))
}

func {>=} (v1,v2) {
    (v1 == v2) or (v1 > v2)
}

func {<=} (v1,v2) {
    (v1 == v2) or (v1 < v2)
}

;; ASSERT

func assert (v, msg) {
    if not v {
        print("assertion error : ")
        println(msg or "no reason given")
        throw(:assert)
    }
}

;; TAG-OR

func tag-or (t1,t2) {
    assert(type(t1) == :tag)
    assert(type(t2) == :tag)
    `:tag $t1.Tag | $t2.Tag`
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; MIN, MAX, BETWEEN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func min (v1, v2) {
    ((v1 < v2) and v1) or v2
}

func max (v1, v2) {
    ((v1 > v2) and v1) or v2
}

func between (min_max, v) {
    max(min_max.0, min(min_max.1, v))
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ITERATORS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

data :Iterator = [f,s,tp,i]

func iter-tuple (it :Iterator) {
    val i = it.i
    if i >= #it.s {
        nil
    } else {
        set it.i = i + 1
        ifs it.tp {
            it == :all -> move([i, it.s[i]])
            it == :idx -> i
            it == :val -> it.s[i]
            else       -> it.s[i]
        }
    }
}

func iter-dict (it :Iterator) {
    val k = next(it.s,it.i)
    if k == nil {
        nil
    } else {
        set it.i = k
        ifs it.tp {
            it == :all -> move([k, it.s[k]])
            it == :key -> k
            it == :val -> it.s[k]
            else       -> it.s[k]
        }
    }
}

func iter-coro (it :Iterator) {
    val co = it.s
    val v = resume co()
    ((status(co) /= :terminated) and v) or nil
}

func iter (v, tp) {
    ifs v {
        it is? :tuple  -> :Iterator [iter-tuple, v, tp, 0]
        it is? :vector -> :Iterator [iter-tuple, v, tp, 0]
        it is? :dict   -> :Iterator [iter-dict,  v, tp, nil]
        it is? :func   -> :Iterator [v]
        it is? :x-coro -> :Iterator [iter-coro,  v]
    }
}

func in' (v, xs) {
    ifs {
        type(xs) == :dict -> not (not xs[v])
        else -> {
            val ok = loop in iter(xs), x {
            } until x == v
            not (not ok)
        }
    }
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TO-*
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func c-to-string (v) {
    assert(type(v) == :pointer)
    val str = #[]
    loop in [0 -> `:number strlen($v.Pointer)`), i {
        set str[+] = `:char ((char*)$v.Pointer)[(int)$i.Number]`
    }
    move(str)
}

func to-string (v) {
    ifs v {
        it is? :pointer -> c-to-string(v)
        it is? :tag -> c-to-string(`:pointer ceu_tag_to_string($v.Tag)`)
        it is? :number -> {
            ```
            static char str[255];
            snprintf(str, 255, "%g", $v.Number);
            ```
            to-string(`:pointer str`)
        }
        else -> v   ;; TODO
    }
}

func to-number (v) {
    ifs v {
        it is? :tag -> `:number $v.Tag`
        ((it is? :vector) and (#it > 0)) and (it[0] is? :char) -> {
            `:number atoi($it.Dyn->Ncast.Vector.buf)`
        }
        else -> it   ;; TODO
    }
}

func to-vector (v) {
    val ret = #[]
    loop in iter(v), i {
        set ret[+] = i
    }
    move(ret)
}

func to-tracks (...) {
    val ret = #[]
    val args = ...
    loop in iter(args), v {
        ifs v.1 {
            it is? :x-task -> {
                set ret[+] = track(it)
            }
            it is? :x-tasks -> {
                loop in :tasks it, i {
                    set ret[+] = i
                }
            }
        }
    }
    move(ret)
}

func vector-to-dict (vect) {
    val dict = @[]
    loop in iter(vect), v {
        set dict[v] = true
    }
    move(dict)
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; CONCAT:  <++  ++
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func {<++} (v1, v2) {
    assert(type(v1) == :vector)
    assert(type(v2) == :vector)
    do {
        loop in [0 -> #v2), i {
            set v1[+] = v2[i]
        }
    }
    v1
}

func {++} (v1, v2) {
    (#[] <++ v1) <++ v2
}

;; composition

func {<|<} (^f,^g) {
    func (v) {
        ^^f(^^g(v))
    }
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; RANDOM:  random-seed  random-next
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func random-seed (n) {
    if n {
        `srand($n.Number);`
    } else {
        `srand(time(NULL));`
    }
}

func random-next () {
    `:number rand()`
}
