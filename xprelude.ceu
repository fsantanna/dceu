;; is', isnot'

func is' (v1,v2) {
    ifs {
        (v1 == v2)         -> true
        (type(v2) /= :tag) -> false
        (type(v1) == v2)   -> true
        tags(v1,v2)        -> true
        else -> false
    }
}

func isnot' (v1,v2) {
    not is'(v1,v2)
}

;; ARITHMETIC:  +  -  *  /  %  **  //

func {+} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number ($v1.Number + $v2.Number)`
}

func {-} (v1, v2) {
    ifs {
        (v1 is :number) and (v2 is :nil) -> {
            `:number (- $v1.Number)`
        }
        (v1 isnot :number) or (v2 isnot :number) -> {
            throw(:error)
        }
        else -> {
            `:number ($v1.Number - $v2.Number)`
        }
    }
}

func {*} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number ($v1.Number * $v2.Number)`
}

func {**} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number powf($v1.Number, $v2.Number)`
}

func {/} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number ($v1.Number / $v2.Number)`
}

func {//} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number powf($v1.Number, 1/$v2.Number)`
}

func {%} (v1, v2) {
    if (v1 isnot :number) or (v2 isnot :number) {
        throw(:error)
    }
    `:number fmod($v1.Number, $v2.Number)`
}

;; RELATIONAL:  >=  >  <=  <

func {>} (v1,v2) {
    ifs {
        (v1 is :tag)    and (v2 is :tag)    -> `:bool ($v1.Tag    > $v2.Tag)`
        (v1 is :number) and (v2 is :number) -> `:bool ($v1.Number > $v2.Number)`
        else -> throw(:error)
    }
}

func {<} (v1,v2) {
    not ((v1 == v2) or (v1 > v2))
}

func {>=} (v1,v2) {
    (v1 == v2) or (v1 > v2)
}

func {<=} (v1,v2) {
    (v1 == v2) or (v1 < v2)
}

;; ASSERT

func assert (v, msg) {
    if not v {
        print("assertion error : ")
        println(msg or "no reason given")
        throw(:assert)
    }
}

;; TAG-OR

func tag-or (t1,t2) {
    assert(type(t1) == :tag)
    assert(type(t2) == :tag)
    `:tag $t1.Tag | $t2.Tag`
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; MIN, MAX
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func min (v1, v2) {
    ((v1 < v2) and v1) or v2
}

func max (v1, v2) {
    ((v1 > v2) and v1) or v2
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ITERATORS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

data :Iterator = [f,v,i]

func iter-tuple (it :Iterator) {
    val i = it.i
    if i >= #it.v {
        nil
    } else {
        set it.i = i + 1
        move([i, it.v[i]])
    }
}

func iter-dict (it :Iterator) {
    val k = next(it.v,it.i)
    if k == nil {
        nil
    } else {
        set it.i = k
        move([k, it.v[k]])
    }
}

func iter-coro (it :Iterator) {
    val co = it.v
    val v = resume co()
    ((co.status /= :terminated) and v) or nil
}

func iter (v) {
    ifs v {
        is :tuple  -> :Iterator [iter-tuple, v, 0]
        is :vector -> :Iterator [iter-tuple, v, 0]
        is :dict   -> :Iterator [iter-dict,  v, nil]
        is :x-coro -> :Iterator [iter-coro,  v]
    }
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; TO-*
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func c-to-string (v) {
    assert(type(v) == :pointer)
    val str = #[]
    loop in [0 -> `:number strlen($v.Pointer)`), i {
        set str[+] = `:char ((char*)$v.Pointer)[(int)$i.Number]`
    }
    move(str)
}

func to-string (v) {
    ifs v {
        is :pointer -> c-to-string(v)
        is :tag -> c-to-string(`:pointer ceu_tag_to_string($v.Tag)`)
        is :number -> {
            ```
            static char str[255];
            snprintf(str, 255, "%g", $v.Number);
            ```
            to-string(`:pointer str`)
        }
        else -> v   ;; TODO
    }
}

func to-number (v) {
    ifs v {
        is :tag -> `:number $v.Tag`
        ((v is :vector) and (#v > 0)) and (v[0] is :char) -> {
            `:number atoi($v.Dyn->Ncast.Vector.buf)`
        }
        else -> v   ;; TODO
    }
}

func to-vector (v) {
    val ret = #[]
    loop in iter(v), i {
        set ret[+] = i
    }
    move(ret)
}

func to-tracks (...) {
    val ret = #[]
    val args = ...
    loop in iter(args), v {
        ifs v.1 {
            is :x-task -> {
                set ret[+] = track(v.1)
            }
            is :x-tasks -> {
                loop in :tasks v.1, i {
                    set ret[+] = i
                }
            }
        }
    }
    move(ret)
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; CONCAT:  <++  ++
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func {<++} (v1, v2) {
    assert(type(v1) == :vector)
    assert(type(v2) == :vector)
    do {
        loop in [0 -> #v2), i {
            set v1[+] = v2[i]
        }
    }
    v1
}

func {++} (v1, v2) {
    (#[] <++ v1) <++ v2
}

;; composition

func {<|<} (^f,^g) {
    func (v) {
        ^^f(^^g(v))
    }
}

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; RANDOM:  random-seed  random-next
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

func random-seed (n) {
    if n {
        `srand($n.Number);`
    } else {
        `srand(time(NULL));`
    }
}

func random-next () {
    `:number rand()`
}
